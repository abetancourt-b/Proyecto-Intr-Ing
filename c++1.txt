Codigo Completo ESP32 Introduccion Ingenieria:

#include <WiFi.h>
#include <time.h>

#include <Wire.h>
#include <Adafruit_Sensor.h>
#include <Adafruit_BMP280.h>

#include "FirebaseESP32.h"
#include <addons/TokenHelper.h>
#include <addons/RTDBHelper.h>

#define WIFI_SSID "repetidor"
#define WIFI_PASSWORD "39727675"

#define API_KEY "AIzaSyAkZoxB2TfujfWVu_8AttZWKNKZqvSY8D4"
#define DATABASE_URL "https://estacion-de-monitoreo-b2335-default-rtdb.firebaseio.com"
#define USER_EMAIL "esp32@test.com"
#define USER_PASSWORD "123456789"

FirebaseData firebaseData;
FirebaseAuth auth;
FirebaseConfig config;

Adafruit_BMP280 bmp;
const int MQ135_PIN = 34;

const int RED = 25;
const int BLUE = 26;
const int GREEN = 27;
const int Buzzer = 32;

const char* ntpServer = "pool.ntp.org";
const long gmtOffset_sec = -18000;
const int daylightOffset_sec = 0;

String getTime() {
  struct tm timeinfo;
  if (!getLocalTime(&timeinfo)) return "SinHora";
  char buffer[50];
  strftime(buffer, sizeof(buffer), "%Y-%m-%d %H:%M:%S", &timeinfo);
  return String(buffer);
}

void setup() {
  Serial.begin(115200);

  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  while (WiFi.status() != WL_CONNECTED) {
    delay(300);
    Serial.print(".");
  }
  Serial.println("\nWiFi conectado.");

  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);

  if (!bmp.begin(0x76)) {
    Serial.println("ERROR: BMP280 no encontrado");
    while (1);
  }

  pinMode(RED, OUTPUT);
  pinMode(BLUE, OUTPUT);
  pinMode(GREEN, OUTPUT);
  pinMode(Buzzer, OUTPUT);

  config.api_key = API_KEY;
  config.database_url = DATABASE_URL;
  auth.user.email = USER_EMAIL;
  auth.user.password = USER_PASSWORD;

  Firebase.begin(&config, &auth);
  while (!Firebase.ready()) delay(200);

  Serial.println("Firebase listo.");
}

void loop() {

  // Lectura MQ135
  int raw = analogRead(MQ135_PIN);
  float voltage = raw * (3.3 / 4095.0);

  // Lectura BMP280
  float Temp = bmp.readTemperature();
  float Presion = bmp.readPressure() / 100.0F;
  float Altitud = bmp.readAltitude(753.0);

  // LÃ³gica de alerta
  String estado_alerta;
  if (Temp < 25) {
    digitalWrite(RED, HIGH);
    digitalWrite(BLUE, HIGH);
    digitalWrite(GREEN, LOW);
    digitalWrite(Buzzer, LOW);
    estado_alerta = "Zona Verde";
  }
  else if (Temp <= 27) {
    digitalWrite(RED, HIGH);
    digitalWrite(BLUE, LOW);
    digitalWrite(GREEN, HIGH);
    digitalWrite(Buzzer, LOW);
    estado_alerta = "Zona Amarilla";
  }
  else {
    digitalWrite(RED, LOW);
    digitalWrite(BLUE, HIGH);
    digitalWrite(GREEN, HIGH);
    digitalWrite(Buzzer, HIGH);
    estado_alerta = "Zona Roja";
  }

  String time = getTime();

  // -----------------------------
  // 1) ESCRIBIR /ESTADO (se sobrescribe)
  // -----------------------------

  Firebase.setInt(firebaseData, "/estado/MQ135/raw", raw);
  Firebase.setFloat(firebaseData, "/estado/MQ135/voltaje", voltage);
  Firebase.setString(firebaseData, "/estado/MQ135/timestamp", time);

  Firebase.setFloat(firebaseData, "/estado/BMP280/temperatura", Temp);
  Firebase.setFloat(firebaseData, "/estado/BMP280/presion_hPa", Presion);
  Firebase.setFloat(firebaseData, "/estado/BMP280/altitud_m", Altitud);
  Firebase.setString(firebaseData, "/estado/BMP280/timestamp", time);

  Firebase.setString(firebaseData, "/estado/alertas/estado", estado_alerta);
  Firebase.setBool(firebaseData, "/estado/alertas/buzzer_activo", (estado_alerta == "Zona Roja"));

  // -----------------------------
  // 2) ESCRIBIR /HISTORIAL (se acumula)
  // -----------------------------

  FirebaseJson jsonMQ;
  jsonMQ.set("raw", raw);
  jsonMQ.set("voltaje", voltage);
  jsonMQ.set("timestamp", time);

  Firebase.pushJSON(firebaseData, "/historial/MQ135", jsonMQ);

  FirebaseJson jsonBMP;
  jsonBMP.set("temperatura", Temp);
  jsonBMP.set("presion_hPa", Presion);
  jsonBMP.set("altitud_m", Altitud);
  jsonBMP.set("timestamp", time);

  Firebase.pushJSON(firebaseData, "/historial/BMP280", jsonBMP);

  Serial.println("Datos enviados correctamente.");

  delay(1500);
}